CREATE VIEW [reporting].[ALL_CREW_EXPERIENCE_VW] AS 

WITH EMPLOYEES AS (
    /* GET GENERAL EMPLOYEE INFORMAITON */
    SELECT 
        EIN,
        JOB_FAMILY,
        MP_CODE,
        LCO_NAME,
        LOB_NAME,
        LEVEL1_NAME,
        CASE 
            WHEN TERM_DATE IS NULL THEN DATEDIFF(D, CONVERT(DATETIME,HIRE_DATE), GETDATE()) 
            ELSE DATEDIFF(D, HIRE_DATE, TERM_DATE) END AS KEY_DAYS_EMPLOYED,
        CASE WHEN TERM_DATE IS NOT NULL THEN 'TERMED' ELSE 'WORKING' END AS EMP_STATUS
    FROM DBO.EMPLOYEES 
)

, RIGS AS (
    /* GET A LIST OF RIGS FROM THE MDM ASSETS TABLE -- NOT AS CONCERNED ABOUT CURRENT STATUS SINCE THIS LOOKS BACK 13 MONTHS*/
    SELECT DISTINCT 
        ASSET_NUM AS RIG
    FROM DBO.MDM_Assets
    WHERE ASSET_CLASS = 'Rig' 
)
, LABOR AS (
    /* GET DAILY LABOR INFORMATION INCLUDING CLIENT AND RIG */
    SELECT 
        L.*
    FROM(
        SELECT DISTINCT 
            EMPLOYEE_ID AS EIN,
            CASE 
                WHEN FORMAT(CONVERT(NUMERIC,ASSET_NUMBER),'0000000') = '0006149' AND CONVERT(DATE,WORK_DATE) = '2024-10-01' THEN '000160'  --ENTRY ERROR IN TABLE 
                WHEN ISNUMERIC(CUSTOMER_ID) = 0 THEN CUSTOMER_ID
                ELSE FORMAT(CONVERT(NUMERIC,CUSTOMER_ID), '000000') 
                END AS CUSTOMER_ID,
            FORMAT(CONVERT(NUMERIC,ASSET_NUMBER),'0000000') AS RIG, 
            CONVERT(DATE,WORK_DATE) AS WORK_DATE,
            DURATION,
            CASE WHEN (LEN(CUSTOMER_ID)> 6 OR ISNUMERIC(CUSTOMER_ID)  = 0) THEN 'CUSTOMER ID ERROR' ELSE '' END AS CUSTOMER_ID_ERROR
        FROM DBO.LABORINCREMENTAL2     
        WHERE CONVERT(DATE,WORK_DATE) > EOMONTH(DATEADD(M, -14, GETDATE())) AND 
            (ISNUMERIC(ASSET_NUMBER) = 1 AND -- REMOVE ASSET NUMBERS THAT CONTAIN LETTERS
            ASSET_NUMBER IS NOT NULL)  -- REMOVE NON ASSET WORK
            
    ) L 
    INNER JOIN RIGS R ON (L.RIG = R.RIG) -- LIMIT LABOR TO JUST RIGS
)

, CLIENT AS (
    /* GET CUSTOMER/ CLIENT NAMES */
    SELECT DISTINCT
        FORMAT(CONVERT(NUMERIC,CUST_NUMBER),'000000') AS CUSTOMER_ID,
        NAME AS CUSTOMER_NAME
    FROM DBO.MDM_CUSTOMERS 
    WHERE STATUS = 1 AND TYPE = 'Customer'
    UNION 
    SELECT '963329' AS CUSTOMER_ID,'DTE GAS RESOURCES' AS CUSTOMER_NAME
    UNION 
    SELECT '000010' AS CUSTOMER_ID, 'YALE AND KEY INC' AS CUSTOMER_NAME 
    UNION
    SELECT '104700' AS CUSTOMER_ID, 'ADS INC' AS CUSTOMER_NAME
    UNION 
    SELECT '089920' AS CUSTOMER_ID, 'OXY' AS CUSTOMER_NAME
    UNION 
    SELECT '022180' AS CUSTOMER_ID, 'CHESAPEAKE OPERATING INC' AS CUSTOMER_NAME
    UNION 
    SELECT '547100' AS CUSTOMER_ID, 'OWENS BROCKWAY GLASS CONTAINER' AS CUSTOMER_NAME
    UNION
    SELECT '963356' AS CUSTOMER_ID, 'DANNY TIDWELL PRODUCTION' AS CUSTOMER_NAME
    UNION 
    SELECT '033170' AS CUSTOMER_ID, 'BIA SOUTHERN UTE AGENCY ROADS' AS CUSTOMER_NAME
    UNION
    SELECT '007565' AS CUSTOMER_ID, 'H V C C LLC' AS CUSTOMER_NAME
    UNION
    SELECT '089520' AS CUSTOMER_ID, 'K-D QUIK STOP INC' AS CUSTOMER_NAME
    UNION
    SELECT '032580' AS CUSTOMER_ID, 'DAWSON GEOPHYSICAL COMPANY' AS CUSTOMER_NAME
    UNION
    SELECT '962673' AS CUSTOMER_ID, 'OCCIDENTAL OF ELK HILLS N/UNIT' AS CUSTOMER_NAME
    UNION
    SELECT '081550' AS CUSTOMER_ID, 'CARIBOU INC' AS CUSTOMER_NAME
    UNION
    SELECT '966340' AS CUSTOMER_ID, 'ALLIED OIL AND GAS INC' AS CUSTOMER_NAME
    UNION
    SELECT '965533' AS CUSTOMER_ID, 'GEP HAYNESVILLE LLC' AS CUSTOMER_NAME
    UNION
    SELECT '077610' AS CUSTOMER_ID, 'EARNEST PRODUCTION COPRORATION' AS CUSTOMER_NAME
    UNION
    SELECT '005130' AS CUSTOMER_ID, 'TEXACO EXPL & PROD INC' AS CUSTOMER_NAME
    UNION
    SELECT '000610' AS CUSTOMER_ID, 'BEPCO LP' AS CUSTOMER_NAME
    UNION
    SELECT '966023' AS CUSTOMER_ID, '4D CONSTRUCTION' AS CUSTOMER_NAME
)

, LABOR_CLIENT AS (
    SELECT 
        L.EIN,
        L.CUSTOMER_ID,
        L.CUSTOMER_ID_ERROR,
        C.CUSTOMER_NAME,
        L.RIG,
        L.WORK_DATE,
        L.DURATION
    FROM LABOR L 
    LEFT JOIN CLIENT C ON (L.CUSTOMER_ID = C.CUSTOMER_ID)
)

, HESS_CREW AS (
    /* IDENTIFY EMPLOYEES THAT WORKED ON HESS RIGS */
    SELECT DISTINCT 
        EMPLOYEE_ID AS EIN, 
        'Y' AS WORKED_HESS
    FROM DBO.LABORINCREMENTAL2
    WHERE CUSTOMER_ID = '000160'
)

, DATASET AS (
    SELECT 
        L.CUSTOMER_ID,
        L.CUSTOMER_ID_ERROR,
        L.CUSTOMER_NAME,
        L.RIG,
        L.WORK_DATE,
        L.DURATION,
        E.*,
        CASE WHEN H.WORKED_HESS IS NULL THEN 'N' ELSE H.WORKED_HESS END AS WORKED_HESS
    FROM LABOR_CLIENT L 
    LEFT JOIN EMPLOYEES E ON (L.EIN = E.EIN)
    LEFT JOIN HESS_CREW H ON (L.EIN = H.EIN)
) 

SELECT * FROM DATASET ;
GO

